apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId 'com.example.hellojni'
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static',
                    // Extra custom variable to
                    // trigger workaround code.
                    '-DHELLOJNI_ANDROID_STUDIO=1'
            }
        }

        splits {
            if (project.hasProperty('arch')) {
                abi {
                    enable true

                    reset()
                    include project.property('arch')

                    universalApk false
                }
            }
            else {
                abi {
                    enable true

                    reset()
                    include 'x86_64', 'armeabi-v7a', 'arm64-v8a'

                    universalApk false
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
             path '../../CMakeLists.txt'
        }
    }
}

dependencies {
    implementation "com.android.support:support-v4:27.0.2"
    implementation "com.android.support:support-v13:27.0.2"
    implementation "com.android.support:cardview-v7:27.0.2"
    implementation "com.android.support:appcompat-v7:27.0.2"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.0.2'
}

ext.abiCodes = ['x86_64':1, 'armeabi-v7a':2, 'arm64-v8a':3]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride =
                    baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}
